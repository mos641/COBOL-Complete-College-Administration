      ******************************************************************
      * Authors: MOSTAPHA A
      * Purpose: TAKES USER INPUT USING SCREENS
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PROJECT03_P2.
       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
           SELECT STUDENT-FILE
             ASSIGN "..\INDEXED-STUFILE3.IND"
                 ORGANIZATION IS INDEXED
                     ACCESS MODE IS RANDOM
                         RECORD KEY IS STUDENT-NUMBER
                             FILE STATUS IS STATUS-FIELD.

       DATA DIVISION.
       FILE SECTION.
      * STUDENT FILE TO BE READ FROMM AND WRITTEN TO
       FD STUDENT-FILE.
       01 STUDENT-RECORD.
           05 STUDENT-NUMBER PIC 9(6).
           05 TUITION-OWED PIC 9(4)V99.
           05 STUDENT-NAME PIC X(40).
           05 PROGRAM-OF-STUDY PIC X(5).
           05 COURSE-CODE-1 PIC X(7).
           05 COURSE-AVERAGE-1 PIC 9(3).
           05 COURSE-CODE-2 PIC X(7).
           05 COURSE-AVERAGE-2 PIC 9(3).
           05 COURSE-CODE-3 PIC X(7).
           05 COURSE-AVERAGE-3 PIC 9(3).
           05 COURSE-CODE-4 PIC X(7).
           05 COURSE-AVERAGE-4 PIC 9(3).
           05 COURSE-CODE-5 PIC X(7).
           05 COURSE-AVERAGE-5 PIC 9(3).

       WORKING-STORAGE SECTION.
       01 CONTROL-FIELDS.
      *    STATUS OF THE RECORD KEY WHEN READ OR WRITTEN
           05 STATUS-FIELD PIC X(2).
      *    IF RECORD IS FOUND
           05 FOUND-FLAG PIC X VALUE "N".

       01 SCREEN-FIELDS.
      *    USER INPUT OF STUDENT NUMBER TO PROCESS PAYMENT FOR
           05 STUDENT-NUMBER-SEARCH PIC 9(6).
      *    USER INPUT OF TUITION PAYMENT PIC9(5)V99 AS SPECIFIED
           05 TUITION-PAYMENT PIC 9(5)V99.
      *    ERROR MESSAGE TO BE DISPLAYED
           05 ERROR-MESSAGE PIC X(30).
      *    EMPTY INPUT FOR SCREEN PROMPT
           05 EMPTY-INPUT PIC X.

       SCREEN SECTION.
      *    ASK FOR STUDENT NUMBER
       01 STUDENT-PROMPT-SCREEN.
           05 VALUE "SEARCH" BLANK SCREEN LINE 1 COL 35.
           05 VALUE "STUDENT NUMBER:" LINE 5 COL 20.
           05 STUDENT-NUMBER-IN LINE 5 COL 36 PIC X(6)
               TO STUDENT-NUMBER-SEARCH.

      *    ASK FOR TUITION PAYMENT
       01 STUDENT-PAYMENT-SCREEN.
           05 VALUE "MAKE A PAYMENT" BLANK SCREEN LINE 1 COL 35.
           05 VALUE "STUDENT NUMBER:" LINE 3 COL 5.
           05 STUDENT-NUMBER-OUT LINE 3 COL 25 PIC X(6)
               FROM STUDENT-NUMBER.
           05 VALUE "STUDENT NAME:" LINE 4 COL 5.
           05 STUDENT-NAME-OUT LINE 4 COL 25 PIC X(40)
               FROM STUDENT-NAME.
           05 VALUE "PROGRAM:" LINE 5 COL 5.
           05 PROGRAM-SCREEN LINE 5 COL 25
                   PIC X(20)  FROM PROGRAM-OF-STUDY.
           05 VALUE "TUITION-OWED:" LINE 6 COL 5.
           05 TUITION-OWED-OUT LINE 6 COL 25 PIC $$$$9.99
               FROM TUITION-OWED.
           05 VALUE "TUITION PAYMENT:" LINE 7 COL 5.
           05 TUITION-PAYMENT-IN LINE 7 COL 25 PIC $9999.99
               TO TUITION-PAYMENT.

      *    DISPLAY THE UPDATED RECORD
       01 STUDENT-UPDATED-SCREEN.
           05 VALUE "UPDATED RECORD" BLANK SCREEN LINE 1 COL 35.
           05 VALUE "STUDENT NUMBER:" LINE 3 COL 5.
           05 STUDENT-NUMBER-SOUT LINE 3 COL 25 PIC X(6)
               FROM STUDENT-NUMBER.
           05 VALUE "STUDENT NAME:" LINE 4 COL 5.
           05 STUDENT-NAME-OUT LINE 4 COL 25 PIC X(40)
               FROM STUDENT-NAME.
           05 VALUE "PROGRAM:" LINE 5 COL 5.
           05 PROGRAM-SCREEN LINE 5 COL 25
                   PIC X(20)  FROM PROGRAM-OF-STUDY.
           05 VALUE "TUITION-OWED:" LINE 6 COL 5.
           05 TUITION-OWED-OUT LINE 6 COL 25 PIC $$$$9.99
               FROM TUITION-OWED.
           05 EXIT-PROMPT LINE 1 COL 34 PIC X
              TO EMPTY-INPUT.

      *    DISPLAY AN ERROR MESSAGE
       01 ERROR-SCREEN.
           05 VALUE "ERROR" BLANK SCREEN LINE 1 COL 35.
           05 ERROR-MESSAGE-SCREEN LINE 5 COL 5 PIC X(30)
               FROM ERROR-MESSAGE.
           05 EXIT-PROMPT LINE 1 COL 34 PIC X
              TO EMPTY-INPUT.

       PROCEDURE DIVISION.
       100-PROCESS-TUITION-PAYMENTS.
           PERFORM 201-INITIALIZE.
           IF FOUND-FLAG EQUALS "Y"
               PERFORM 202-PROCESS-PAYMENT
           END-IF.
           PERFORM 203-TERMINATE.
           STOP RUN.

      * OPENS THE FILE AND TAKES SEARCHES FOR USER SUBMITTED STUDENT NUMBER
       201-INITIALIZE.
           PERFORM 301-OPEN-FILES.
           PERFORM 302-STUDENT-PROMPT-SCREEN.
           PERFORM 303-FIND-STUDENT.

      * TAKES USER INPUT FOR PAYMENT AMOUNT AND UPDATES RECORD
       202-PROCESS-PAYMENT.
           PERFORM 304-PAYMENT-PROMPT-SCREEN.
           PERFORM 305-COMPUTE-TUITON-OWED.
           PERFORM 306-UPDATE-STUDENT-RECORD.
           PERFORM 307-DISPLAY-UPDATE.

      * CLOSES THE FILE
       203-TERMINATE.
           CLOSE STUDENT-FILE.

       301-OPEN-FILES.
           OPEN I-O STUDENT-FILE.

       302-STUDENT-PROMPT-SCREEN.
           DISPLAY STUDENT-PROMPT-SCREEN
           ACCEPT STUDENT-PROMPT-SCREEN.

       303-FIND-STUDENT.
           MOVE STUDENT-NUMBER-IN TO STUDENT-NUMBER.
           READ STUDENT-FILE
           INVALID KEY
               MOVE "STUDENT NOT FOUND" TO ERROR-MESSAGE
               DISPLAY ERROR-SCREEN
               ACCEPT ERROR-SCREEN
               MOVE "N" TO FOUND-FLAG
           NOT INVALID KEY
               MOVE "Y" TO FOUND-FLAG
           END-READ.

       304-PAYMENT-PROMPT-SCREEN.
           DISPLAY STUDENT-PAYMENT-SCREEN
           ACCEPT STUDENT-PAYMENT-SCREEN.

       305-COMPUTE-TUITON-OWED.
           IF TUITION-PAYMENT IS GREATER THAN TUITION-OWED
              COMPUTE TUITION-OWED = 0
           ELSE
               COMPUTE TUITION-OWED = TUITION-OWED - TUITION-PAYMENT
           END-IF.

       306-UPDATE-STUDENT-RECORD.
           REWRITE STUDENT-RECORD
           INVALID KEY
               MOVE "N" TO FOUND-FLAG
           NOT INVALID KEY
               MOVE "Y" TO FOUND-FLAG
           END-REWRITE.

       307-DISPLAY-UPDATE.
           IF FOUND-FLAG EQUALS "Y"
               DISPLAY STUDENT-UPDATED-SCREEN
               ACCEPT STUDENT-UPDATED-SCREEN
           ELSE
               MOVE "STUDENT UNABLE TO BE UPDATED" TO ERROR-MESSAGE
               DISPLAY ERROR-SCREEN
               ACCEPT ERROR-SCREEN.

       END PROGRAM PROJECT03_P2.
