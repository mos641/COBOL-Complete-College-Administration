      ******************************************************************
      * Authors: MOSTAPHA A
      * Purpose: READS STUDENT AND COURSE RECORDS FROM EXTERNAL FILES
      *          AND WRITES STUDENT REPORT TO AN EXTERNAL FILE
      ******************************************************************
       IDENTIFICATION DIVISION.
       PROGRAM-ID. PROJECT03_P3.

       ENVIRONMENT DIVISION.
       INPUT-OUTPUT SECTION.
       FILE-CONTROL.
      *        EXTERNAL FILE WITH STUDENT RECORDS
               SELECT STUDENT-FILE-IN
                   ASSIGN TO "../INDEXED-STUFILE3.IND"
                       ORGANIZATION IS INDEXED
                           ACCESS MODE IS SEQUENTIAL
                               RECORD KEY IS STUDENT-NUMBER
                                  FILE STATUS IS STATUS-FILED.

      *        EXTERNAL FILE WITH PROGRAM NAMES
               SELECT PROGRAM-FILE-IN
                   ASSIGN TO "../PROGRAM.TXT"
                       ORGANIZATION IS LINE SEQUENTIAL.

      *        EXTERNAL FILE TO WRITE STUDENT REPORTS TO
               SELECT STUDENT-REPORT-FILE-OUT
                   ASSIGN TO "../STUDENT-REPORT.TXT"
                       ORGANIZATION IS LINE SEQUENTIAL.

       DATA DIVISION.
       FILE SECTION.
      * STUDENT RECORD READ FROM EXTERNAL FILE
       FD STUDENT-FILE-IN.
       01 STUDENT-RECORD-IN.
           05 STUDENT-NUMBER PIC 9(6).
           05 TUITION-OWED PIC 9(4)V99.
           05 STUDENT-NAME PIC X(40).
           05 PROGRAM-OF-STUDY PIC X(5).
           05 COURSE-CODE-1 PIC X(7).
           05 COURSE-AVERAGE-1 PIC 9(3).
           05 COURSE-CODE-2 PIC X(7).
           05 COURSE-AVERAGE-2 PIC 9(3).
           05 COURSE-CODE-3 PIC X(7).
           05 COURSE-AVERAGE-3 PIC 9(3).
           05 COURSE-CODE-4 PIC X(7).
           05 COURSE-AVERAGE-4 PIC 9(3).
           05 COURSE-CODE-5 PIC X(7).
           05 COURSE-AVERAGE-5 PIC 9(3).

      * PROGRAM RECORD READ FROM EXTERNAL FILE
           COPY './TABLE-COPY.TXT'.

      * STUDENT RECORD WRITTEN TO AN EXTERNAL FILE
       FD STUDENT-REPORT-FILE-OUT.
       01 STUDENT-RECORD-OUT PIC X(130).

       WORKING-STORAGE SECTION.
      * FORMATTED RECORD STRUCTURE TO WRITE TO EXTERNAL FILE
       01 STUDENT-REPORT-RECORD-OUT.
           05 STUDENT-NAME-OUT PIC X(40).
           05 FILLER PIC X(3) VALUE SPACES.
           05 STUDENT-AVERAGE-OUT PIC ZZ9.
           05 FILLER PIC X(4) VALUE "%".
           05 PROGRAM-NAME-OUT PIC X(20).
           05 FILLER PIC X(4) VALUE SPACES.
           05 TUITION-OWED-OUT PIC $$,$$9.99.

      * HEADER TO WRITE TO FILE FOR FORMATTING PURPOSES
       01 STUDENT-REPORT-HEADER.
           05 FILLER PIC X(42) VALUE "NAME".
      *     05 FILLER PIC X(3) VALUE SPACES.
           05 FILLER PIC X(8) VALUE "AVERAGE".
           05 FILLER PIC X(24) VALUE "PROGRAM".
           05 FILLER PIC X(12) VALUE "TUITION OWED".

      * LINE TO SEPARATE HEADER FROM DATA
       01 HEADER-LINE.
           05 FILLER PIC X(130) VALUE ALL "-".

      * TABLE TO STORE PROGRAM DATA FOR EASIER SEARCHING
       01 PROGRAM-DATA.
           05 PROGRAM-TABLE OCCURS 20 TIMES.
               10 PROGRAM-CODE-CLM PIC X(5).
               10 PROGRAM-NAME-CLM PIC X(20).

       01 CONTROL-FIELDS.
      *    END OF FILE FLAGS
           05 EOF-FLAG-STU PIC A.
           05 EOF-FLAG-PRO PIC A.
      *    COUNTERS FOR AUDITING
           05 READ-COUNTER PIC 9(2) VALUE 0.
           05 WRITE-COUNTER PIC 9(2) VALUE 0.
      *    SUBSCRIPT TO ACCESS TABLE COLUMNS
           05 SUB PIC 9(2).
      *    FLAG FOR SEARCHING
           05 FOUND-FLAG PIC A.
      *    USED IN EXTERNAL ROUTINE
           05 AVERAGE-GRADE PIC 9(3).
           05 STATUS-FILED PIC X(2).

       PROCEDURE DIVISION.
       100-CREATE-STUDENT-REPORTS.
      *    OPEN FILES AND STORES PROGRAM DATA
           PERFORM 201-INITIALIZE-STUDENT-REPORTS.
      *    CREATE AND WRITE A SINGLE STUDENT REPORT
           PERFORM 202-CREATE-STUDENT-REPORT
      *    LOOP UNTIL END OF FILE IS REACHED
               UNTIL EOF-FLAG-STU EQUALS "Y".
      *    DISPLAY AUDIT AND CLOSE FILES
           PERFORM 203-TERMINATE.
      *    END PROGRAM
           STOP RUN.

      * OPEN FILES AND STORES PROGRAM DATA
       201-INITIALIZE-STUDENT-REPORTS.
           PERFORM 301-OPEN-FILES.
           PERFORM 302-LOAD-PROGRAM-TABLE
               VARYING SUB FROM 1 BY 1 UNTIL SUB > 20 OR
               EOF-FLAG-PRO = "Y".
           PERFORM 303-WRITE-REPORT-HEADER.
           PERFORM 304-READ-STUDENT-RECORD.

      * CREATE AND WRITE A SINGLE STUDENT REPORT
       202-CREATE-STUDENT-REPORT.
      *    RESET FOUND FLAG FOR SEARCHING
           MOVE "N" TO FOUND-FLAG.
      *    SEARCH FOR A PROGRAM UNTIL FOUND OR MAXIMUM IS REACHED
           PERFORM 305-SEARCH-PROGRAMS-TABLE
               VARYING SUB FROM 1 BY 1 UNTIL SUB > 20
               OR FOUND-FLAG = "Y".
      *    CALCULATE THE STUDENTS AVERAGE MARK
           PERFORM 306-CALCULATE-AVERAGE.
      *    WRITE STUDENT REPORT TO FILE
           PERFORM 307-WRITE-STUDENT-REPORT.
      *    READ THE NEXT STUDENT RECORD
           PERFORM 304-READ-STUDENT-RECORD.

      * DISPLAY AUDIT AND CLOSE FILES
       203-TERMINATE.
           PERFORM 308-DISPLAY-RECORDS-COUNT.
           PERFORM 309-CLOSE-FILES.

      * OPEN THE EXTERNAL FILES USED
       301-OPEN-FILES.
           OPEN INPUT  STUDENT-FILE-IN
                       PROGRAM-FILE-IN.
           OPEN OUTPUT STUDENT-REPORT-FILE-OUT.

      * READ PROGRAM NAMES AND CODES INTO DEFINED TABLE
       302-LOAD-PROGRAM-TABLE.
      *    IF THE END OF FILE IS REACHED SET FLAG OTHERWISE STORE DATA
           READ PROGRAM-FILE-IN
           AT END
               MOVE "Y" TO EOF-FLAG-PRO
           NOT AT END
               MOVE PROGRAM-CODE TO PROGRAM-CODE-CLM(SUB)
               MOVE PROGRAM-NAME TO PROGRAM-NAME-CLM(SUB).

      * WRITE THE HEADER OF THE STUDENTS REPORTS FILE
       303-WRITE-REPORT-HEADER.
           WRITE STUDENT-RECORD-OUT FROM STUDENT-REPORT-HEADER.
           WRITE STUDENT-RECORD-OUT FROM HEADER-LINE.

      * READ STUDENT RECORD FROM EXTERTNAL FILE
       304-READ-STUDENT-RECORD.
      *    IF THE END OF FILE IS REACHED SET FLAG OR ADD TO COUNTER
           READ STUDENT-FILE-IN
           AT END
               MOVE "Y" TO EOF-FLAG-STU
           NOT AT END
               ADD 1 TO READ-COUNTER.

      * SEARCH PROGRAMS TABLE FOR SPECIFIED PROGRAM CODE
       305-SEARCH-PROGRAMS-TABLE.
           IF PROGRAM-OF-STUDY EQUALS PROGRAM-CODE-CLM(SUB)
               MOVE PROGRAM-NAME-CLM(SUB) TO PROGRAM-NAME-OUT
               MOVE "Y" TO FOUND-FLAG
           END-IF.

      * CALCULATE THE STUDENTS AVERAGE MARK
       306-CALCULATE-AVERAGE.
           CALL './PROJECT03_P3_SUB'
               USING AVERAGE-GRADE, COURSE-AVERAGE-1,
               COURSE-AVERAGE-2, COURSE-AVERAGE-3
               COURSE-AVERAGE-4, COURSE-AVERAGE-5
           END-CALL.
           MOVE AVERAGE-GRADE TO STUDENT-AVERAGE-OUT.

      * WRITE STUDENT REPORT TO EXTERNAL FILE
       307-WRITE-STUDENT-REPORT.
           MOVE STUDENT-NAME TO STUDENT-NAME-OUT
           MOVE TUITION-OWED TO TUITION-OWED-OUT
           WRITE STUDENT-RECORD-OUT FROM STUDENT-REPORT-RECORD-OUT.
           ADD 1 TO WRITE-COUNTER.

      * DISPLAY AUDIT
       308-DISPLAY-RECORDS-COUNT.
           DISPLAY READ-COUNTER " RECORDS READ".
           DISPLAY WRITE-COUNTER " RECORDS WRITTEN".

      * CLOSE THE EXTERNAL FILES USED
       309-CLOSE-FILES.
           CLOSE   PROGRAM-FILE-IN
                   STUDENT-FILE-IN
                   STUDENT-REPORT-FILE-OUT.

       END PROGRAM PROJECT03_P3.
